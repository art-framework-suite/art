#!/usr/bin/perl -w
########################################################################
# filter-output.
#
# Filter out some things that would affect the utility of a comparison
# with reference.
#
# If this is not sufficient for your needs, write your own and use the
# OUTPUT_FILTER option of cet_test() to override. See CetTest.cmake
# documentation for details.
#
# 2014/12/16 CHG.
########################################################################
use strict;

my ($in_tt, $in_mm, $mm_format, $final_mm_clause, $in_ev_list);
my $n_sep = 0;

while (<>) {
  ########################################################################
  # Several date-time formats.
  s&\d{4}-(?:0\d|1[0-2])-(?:[0-2]\d|3[01])T(?:[01]\d|2[0-3]):[0-5]\d:[0-5]\d(?:,\d+)?(Z[-+]?\d+)?&<date-time>&g;
  s&\d{4}-(?:0\d|1[0-2])-(?:[0-2]\d|3[01])(?:\s+(?:[01]\d|2[0-3]):[0-5]\d(?::[0-5]\d(?:\.\d+)?(?:\s+(?:[-+]\d+|[A-Z]{3}))?)?)?&<date-time>&g;
  s&\d+-(Jan|Feb|Mar|Apr|May|Jun|Jul|Aug|Sep|Oct|Nov|Dec)-\d{4}(?:\s+(?:[01]\d|2[0-3]):[0-5]\d(?::[0-5]\d(?:\.\d+)?(?:\s+(?:[-+]\d+|[A-Z]+))?)?)?&<date-time>&g;
  ########################################################################
  # Messagefacility messages.
  s&^(\%MSG.*?:\s+).*?(<date-time>)&${1}${2}&; # OS X does not currently print application.
  ########################################################################
  # TimeReport lines.
  m&^TimeReport& and s&[\d\.]+&<duration>&g;
  ########################################################################
  # TimeTracker summary lines
  if ($in_tt) {
    if (m&^={20}& and ++$n_sep == 2) {
      $n_sep = 0;
      undef $in_tt;
      next;
    }
    s&^((?:Full event|\S+))\s+(?:\S+\s+){5}&${1}\t<duration> <duration> <duration> <duration> <duration> &;
  }
  m&^TimeTracker printout& and $in_tt = 1;
  ########################################################################
  # MemoryTracker summary lines
  if ($in_mm) {
    if ($final_mm_clause and m&^={20}& and ++$n_sep == 2) {
      # Done. Clean up.
      undef $in_mm;
      undef $final_mm_clause;
      undef $mm_format;
      $n_sep = 0;
    } elsif ($mm_format == 1) {
      s&\s+(Peak virtual memory usage \(VmPeak\):)\s+(.*)$&${1}\t<mem-size>&;
      s&^(Module \S+)\s+(\S+)\s+.*$&${1}\t${2}\t<mem-size>\t<mem-size>&;
    } else { # $mm_format == 2
      if (m&^\s*run&) {
        if (not $in_ev_list) {
          $in_ev_list = 1;
        }
        next;
      } else {
        if ($in_ev_list) {
          undef $in_ev_list;
          print " <ev-info>\n";
        }
      }
    }
  }
  if (m&^MemoryTracker General SUMMARY&) {
    $in_mm = 1;
    $mm_format = 1;
  } elsif ($in_mm and m&^MemoryTracker Per-event SUMMARY&) {
    $mm_format = 2;
  } elsif ($in_mm and m&^Modules with large Vsize&) {
    $final_mm_clause = 1;
  }
  ########################################################################
  # Variable-length separators.
  s&([-=\.*~/]){15,}&<separator (${1})>&g;
  ########################################################################
  print;
}
